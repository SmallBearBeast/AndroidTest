plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // 引入Gradle相关Api
    implementation gradleApi()
//    implementation localGroovy()
    // AGP自带asm
    implementation 'com.android.tools.build:gradle:7.3.0'
    // 引入kotlin标准库
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.10'
    // common library used by other Android tools libraries.
    implementation 'com.android.tools:common:30.1.0'

    // 引入asm相关依赖
    implementation 'org.ow2.asm:asm:9.1'
    implementation 'org.ow2.asm:asm-util:9.1'
    implementation 'org.ow2.asm:asm-commons:9.1'
}

// 配置 Gradle 插件但不自动发布
gradlePlugin {
    plugins {
        customtransformplugin {
            id = 'CustomTransformPlugin'
            implementationClass = 'com.example.plugintest.CustomTransformPlugin'
        }
    }
    // 禁用自动发布插件到 Gradle Plugin Portal
    automatedPublishing = false
}

// 创建源码 jar 任务
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

//配置发布本地 maven
publishing {
    repositories {
        maven { url uri("${rootProject.projectDir}/repo") }
    }
    publications {
        debug(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'com.example'
            artifactId 'customtransformplugin'
            version '1.0.2'
        }
    }
}